/*
 * Copyright 2020 Robin Mercier, Alexandru Galetus
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'application'
}

ext.moduleName = 'discord-bot'
archivesBaseName = moduleName

dependencies {
    implementation project(':bot-library')
}

java {
    mainClassName = 'me.montai.discord.bot.Main'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task sourcesForRelease(type: Copy) {
    from('src/main/java') {
        include('**/Config.java')
        filter(ReplaceTokens, tokens: [
                token: getProjectProperty('discordToken')
        ])
    }
    into('build/filteredSrc')
    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'Config.java'
    }.asFileTree
    source = javaSources + fileTree(sourcesForRelease.destinationDir)
    dependsOn(sourcesForRelease)
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from('src/main/java') {
        exclude('**/Config.java')
    }
    from(sourcesForRelease.destinationDir)
    dependsOn(sourcesForRelease)
}

compileJava {
    source = generateJavaSources.source
    dependsOn(generateJavaSources)
}

String getProjectProperty(String key) {
    return project.properties.getOrDefault(key, "")
}